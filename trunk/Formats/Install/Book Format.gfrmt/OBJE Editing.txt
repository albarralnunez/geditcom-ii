! Link to the multimedia object
if @contents is validlink
  #padding=3
  if @contents.FORM="url"
    ! Link to the web page
    newline
    cell Multimedia
    set url @contents._FILE font "Hyperlink Text"
    if printing
      fontcolor black
    endif
    if @contents.TITL<>""
      TextExpression @contents.TITL
    else
      TextExpression @contents._FILE
    endif
    set objectImage @contents imageposition left imageheight 22
	sizetofit -1
    set alignment left height 22 offset #buttonOffset
    set help local("Click to open in browser")

    ! rest in a child window
    if printing is false
      HMoveTo #rightMargin$-#rightSkip-60-#cellSkip
      cell LinkButton
      set border no image "details_here"
      RecordLink @contents
      set target child
      sizetofit
      shift 60-#cellWidth$
      Help local("Click to view and edit multimedia object")
    endif

  else
    #thumbW=int((#rightMargin$-#rightSkip-#leftSkip-(#mmPerRow-1)*#cellSkip)/#mmPerRow)
    cell LinkButton
	set width #thumbW
	if #mmPerRow>1
      objectImage @contents
      #mmScale=1
    else
      objectFull @contents
      #mmScale=@contents._SCALE
	  if #mmScale=""
	    #mmScale=1
	  endif
    endif
    #mmHeight=#imageHeight$*#mmScale
    #mmWidth=#imageWidth $*#mmScale
    #posWidth=#thumbW-2*#padding
    if #mmWidth<#posWidth
      ! use actual image (or thumbnail) width and set height to hold actual image height
      set imageWidth #mmWidth
      set height #mmHeight+2*#padding+22
    else
      ! scale down image to fit in #posWidth
      set imageWidth #posWidth
	  set height #mmHeight*#posWidth/#mmWidth+2*#padding+22
    endif

	! title in the same button
	TextExpression @contents.rec
	AppendedExpression view
	autoreload yes
	if @contents.TITL=""
	  text @contents._FILE
	else
	  text @contents.TITL
	endif

	set imageposition above
    set border no target child
    RecordLink @contents
    Help local("Click to view and edit multimedia object")
  endif

else
  ! embedded multimedia
  newline
  cell static,local("Embedded multimedia (reopen file to fix)")
  sizetofit -1

endif

! should not be any details, but hide in case
hideexcess


