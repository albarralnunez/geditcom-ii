Load ListBlock,ResetTabs,RefLinks,References,ToolBtn,OBJEPanes

! -------------- Start Template -----------------
GoSub ResetTabs
DropTypes INDI,FAM

#title=@TITL
#path=@_FILE
#form=@FORM

Gosub OBJEPanes,"Notes","Yes"

newline
if #form="url"
  cell Multimedia
  set url #path width -1 font "Hyperlink Text"
  if #title<>""
    text #title
  else
    text #path
  endif
  set objectImage @this imageposition left
  sizetofit -1

  set alignment left height #imageHeight$
  set help local("Click to open this URL link in you web browser") 
  newline
else
  newline
  cell Multimedia
  #padding=5
  #thumbMax=96
  set objectImage @this mmFormat #form width -1
  set imageposition imageonly border shadowlesssquare offset 3
  #mmRatio=#imageHeight$/#imageWidth$
  if #mmRatio<1
    set imageWidth #thumbMax
  else
    set imageheight #thumbMax
  endif
  set height #imageHeight$+2*#padding
  set width #imageWidth$+2*#padding

  ! handle non-image objects
  if #imageType$="noimage"
    set text local("No image representation. Click to open in another application")
    set font "Static Text" imageposition left alignment left
    help local("Click to open in a helper application.")
	help " "&local("You can configure which application opens each type of multimedia object.")
  else if #imageType$="notfound"
    set text " "&local("Multimedia file not found (click to fix)")
    set font "Static Text" imageposition left alignment left
    set MenuLink "Attach Multimedia"
	help local("Click to link this record to a multimedia object")
  else if #imageType$="image"
    help local("Click to open in a helper application.")
	help " "&local("You can configure which application opens each type of multimedia object.")
  else
    !thumbnail
    help local("Click to view multimedia record")
    set recordlink @this paneID ""
  endif
  hskip 5
endif

#newLeft=#hpos$
#currentPos=#vpos$
newline
#extraPos=#vpos$
if @_REF<>""
  #leftSkip=#newLeft
  MoveTo #leftSkip,#currentPos
  ClearTabs
  SetTab #newLeft+100
  cell label,local("Linked Records")
  SizeToFit
  newline -3
  cell GroupBox
  set width -1 border line titleposition none
  #leftSkip=#leftSkip$
  newline -#lineSkip-3
  gosub RefLinks,"Linked to","All Linked Record"
  EndGroup
  GoSub ResetTabs
endif

#leftSkip=#leftSkip$
newline
if #vpos$<#extraPos
  MoveTo #leftSkip,#extraPos
endif

! --------- NOTEs, SOURs, OBJEs, and _LOGs -----------
gosub References,"_LOG"

hide _LOC,_KEY,_FILE,FORM,_SCALE

! -------- Additional Data ------------
if more is true
  ClearTabs
  SetTab 150,240,330
  cell label,local("Additional Data")
  SizeToFit
  newline -3
  cell GroupBox
  set width -1 border line titleposition none
  newline -#lineSkip-3
  ShowAll CHAN
  ShowAll all
  EndGroup
endif

optionalTag RIN,local(RIN)
optionalTag REFN,local(REFN)
optionalTag _KEY,local(_KEY)

DropTypes NOTE,SOUR,LOG,INDI,FAM


